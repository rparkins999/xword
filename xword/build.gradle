/*
 * Copyright Â© 2023. Richard P. Parkins, M. A.
 * Released under GPL V3 or later
 */

plugins {
    id 'com.android.application'
}

def keystorePropertiesFile = rootProject.file("../Keys/keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

def gitdata1 = new ByteArrayOutputStream()
def gitdata2 = new ByteArrayOutputStream()
def gitdata3 = new ByteArrayOutputStream()
// change this line to /* if not using git
exec {
    commandLine "git", "--no-pager", "show", "-s",
            "--format=from commit %H"
    standardOutput = gitdata1
}
exec {
    commandLine "git", "--no-pager", "show", "-s",
            "--date=format:%H:%M:%S GMT%z %a %b %d %Y",
            "--format=of %ad"
    standardOutput = gitdata2
}
exec {
    commandLine "git", "branch", "--show-current"
    standardOutput = gitdata3
}
//*/

// Update these three to make new version
def VERSIONCODE = 4
def VERSIONNAME = "1.3"
def CHANGESTRING = "Fake hint so it works with hardware keyboard"

def CHANGELOGS = "fastlane/metadata/android/en-US/changeLogs/"
def CHANGEFILE = CHANGELOGS + VERSIONCODE + ".txt"
exec {
    commandLine "echo", "${CHANGESTRING.toString()}"
    standardOutput = new FileOutputStream(rootProject.file(CHANGEFILE))
}
exec {
    commandLine "git", "add", "../" + "${CHANGELOGS.toString()}"
}

android {
    namespace 'uk.co.yahoo.p1rpp.xword'
    compileSdk 33

    signingConfigs {
        config {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }

    defaultConfig {
        applicationId "uk.co.yahoo.p1rpp.xword"
        minSdk 24
        targetSdk 33
        versionCode VERSIONCODE
        versionName VERSIONNAME
    }

    buildTypes {
        release {
            minifyEnabled false
            signingConfig signingConfigs.config
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            delete("build/intermediates/incremental/mergeReleaseAssets/merger.xml")
            resValue("string", 'build_time',
                    "${(new Date()).format('HH:mm:ss \'GMT\'Z EEE dd MMM yyyy')}")
            resValue("string", 'build_git1', "${gitdata1.toString()}")
            resValue("string", 'build_git2', "${gitdata2.toString()}")
            resValue("string", 'build_git3',
                    "${" on branch " + gitdata3.toString()}")
        }
        debug {
            minifyEnabled false
            signingConfig signingConfigs.config
            jniDebuggable true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            delete("build/intermediates/incremental/mergeDebugAssets/merger.xml")
            resValue("string", 'build_time',
                    "${(new Date()).format('HH:mm:ss \'GMT\'Z EEE dd MMM yyyy')}")
            resValue("string", 'build_git1', "${gitdata1.toString()}")
            resValue("string", 'build_git2', "${gitdata2.toString()}")
            resValue("string", 'build_git3',
                    "${" on branch " + gitdata3.toString()}")
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    externalNativeBuild {
        cmake {
            path file('src/main/cpp/CMakeLists.txt')
            version '3.22.1'
        }
    }
    buildFeatures {
        viewBinding true
    }
}

dependencies {
    implementation 'com.google.android.material:material:1.8.0'
}
